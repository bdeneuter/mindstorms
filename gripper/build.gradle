plugins {
    id "com.github.johnrengelman.shadow" version "1.2.1"
}

configurations {
    scp
}

dependencies {
    compile project(':robot')
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    provided project(':ev3classes')

    scp 'org.apache.ant:ant-jsch:1.9.4'
}

jar {
    manifest {
        attributes('Class-Path':
                'home/root/lejos/lib/ev3classes.jar ' +
                        '/home/root/lejos/lib/dbusjava.jar ' +
                        '/home/root/lejos/libjna/usr/share/java/jna.jar')
    }
}

assemble.dependsOn shadowJar

task copyToRobot(dependsOn: 'assemble') {
    doLast {

        ant.taskdef(
                name: 'scp',
                classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
                classpath: configurations.scp.asPath)

        ant.scp(
                file: new File(buildDir, "libs/gripper-all.jar"),
                todir: brick_user + '@' + brick_host + ':' + brick_home,
                username: brick_user,
                password: '',
                trust: true
        )
    }
}

